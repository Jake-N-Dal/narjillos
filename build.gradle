allprojects {
	version = new File('version').getText()

	apply plugin: 'java'
	apply plugin: 'application'
	apply plugin: 'eclipse'
	apply plugin: 'idea'

  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

	eclipse {
	  jdt {
		  // Eclipse will use JDK 1.8 if it's been configured for that,
			// but it's still stuck in a Java 1.7 world
			sourceCompatibility = JavaVersion.VERSION_1_7
  		targetCompatibility = JavaVersion.VERSION_1_7
	  }
	}

	startScripts.enabled = false
	run.enabled = false

	repositories {
		mavenCentral()
	}

	dependencies {
		compile subprojects
		testCompile 'junit:junit:4.11'
	}

	task wrapper(type: Wrapper) {
		gradleVersion = '2.0'
	}
	
	jar {
			// skip empty JARs
	    onlyIf { !sourceSets.main.allSource.files.isEmpty() }
	}

  createStartScript('narjillos', project, 'org.nusco.narjillos.PetriDish', [])
  createStartScript('petri', project, 'org.nusco.narjillos.PetriDish', ['-Xmx4096M', '-Xms4096M'])
  createStartScript('experiment', project, 'org.nusco.narjillos.Lab', ['-Xmx4096M', '-Xms4096M'])
}

project(':core') {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

	jar {
		baseName = 'narjillos-core'
	}

	dependencies {
		compile 'com.google.code.gson:gson:2.3'
	}
}

project(':petridish') {
	jar {
		baseName = 'narjillos-petridish'
		mainClassName = 'org.nusco.narjillos.PetriDish'
	}

	dependencies {
		compile project(':core')
	}
}

def createStartScript(name, project, mainClass, jvmArgs) {
  def taskName = name + "StartScript"
  project.tasks.create(name: taskName, type: CreateStartScripts) {
    outputDir       = new File(project.buildDir, 'scripts')
    mainClassName   = mainClass
    applicationName = name
    classpath       = project.tasks[JavaPlugin.JAR_TASK_NAME].outputs.files + project.configurations.runtime
    applicationDefaultJvmArgs = jvmArgs
  }
	
  project.tasks[taskName].dependsOn(project.jar)

  project.applicationDistribution.with {
    into("bin") {
      from(project.tasks[taskName])
      fileMode = 0755
    }
  }
}
